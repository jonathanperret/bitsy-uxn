@parser
  ( filename* -- )
  &parse
    DUP2 P< "Loading 20 >P ;print JSR2 LF

    ;&open-file JSR2

    &next-line
      ;&get-line JSR2 ,&eof JCN

      ;&check-not-type JSR2 "PAL ,&not-pal JCN
        ;&read-palette JSR2
      &not-pal

      ;&check-not-type JSR2 "SET ,&not-set JCN
        ;&read-set JSR2
      &not-set

      ;&check-not-type JSR2 "TIL ,&not-til JCN
        ;&read-tile JSR2
      &not-til

      ;&check-not-type JSR2 "SPR ,&not-spr JCN
        ;&read-sprite JSR2
      &not-spr

      ;&check-not-type JSR2 "DLG ,&not-dlg JCN
      &not-dlg

      ;&next-line JMP2

    &eof

    RTN

  ( -- notmatch? )
  &check-not-type
    STH2rk 3++r ( copy and adjust return address )
    LDA2k STH2 ;&line STH2k LDA2r NEQ2r
    INC2 LDA STH
    INC2 LDA STHr NEQ
    STHr ORA
    RTN

  ( -- )
  &read-palette
    #0000 [ ;&pal/r STA2 ]
    #0000 [ ;&pal/g STA2 ]
    #0000 [ ;&pal/b STA2 ]

    #0000 #0003 DO
      DUP #60 SFT #04 ADD STH

      ;&pal/r LDA2k ;&read-number JSR2 POP STHrk SFT2 ORA2 SWP2 STA2
      ;&pal/g LDA2k ;&read-number JSR2 POP STHrk SFT2 ORA2 SWP2 STA2
      ;&pal/b LDA2k ;&read-number JSR2 POP STHrk SFT2 ORA2 SWP2 STA2

      POPr
    LOOP

    [ ;&pal/r LDA2 ] .System/r DEO2
    [ ;&pal/g LDA2 ] .System/g DEO2
    [ ;&pal/b LDA2 ] .System/b DEO2
    RTN
    [ &pal/r $2 &pal/g $2 &pal/b $2 ]

  ( -- )
  &read-tile
    ;&line 4++ LDA #60 SUB

    ( tile-id )

    #00 SWP 8** ;game/tiles ADD2

    ( tile-data* )
    ;&read-bitmap JMP2

  ( -- )
  &read-sprite
    ;&line 4++ LDA LIT 'A SUB
    DUP #20 LTH ,&read-sprite/avatar JCN
    #1f SUB
    &read-sprite/avatar

    ( sprite-id )

    DUP #00 SWP 8** ;game/sprites ADD2
    ( sprite-data* )

    ;&read-bitmap JSR2

    #06 ;&skip-bytes JSR2

    ;&read-number JSR2 POP NIP
    ;&read-number JSR2 POP NIP
    ( sprite-id x y )
    ROT
    ( x y sprite-id )
    #00 SWP 2** ;game/sprite-pos ADD2
    ( x y spritepos* )
    STA2

    [ ;game/sprite-count *INC ]
    RTN

  ( n -- )
  &skip-bytes
    ;&get-byte JSR2 POP
    DEC DUP ,&skip-bytes JCN
    POP
    RTN

  ( data* -- )
  &read-bitmap
    STH2
    ( : tile-data* )
    #08
    &read-tile/loop
      ;&read-binary-byte JSR2 POP
      STH2rk STA
      INC2r
      DEC DUP ,&read-tile/loop JCN
    POP
    POP2r
    RTN

  ( -- n success )
  &read-binary-byte
    LITr 00
    #00 [ ;&read-binary-byte/started STA ]
    &read-binary-byte/loop
      ;&get-byte JSR2
      DUP LIT '0 LTH ,&read-binary-byte/done JCN
      #01 [ ;&read-binary-byte/started STA ]
      LIT '0 SUB ( value : n )
      2*r STH ADDr ( : n*2+value )
      ,&read-binary-byte/loop JMP
    &read-binary-byte/done
    ( terminator )
    POP
    STHr
    LIT [ &read-binary-byte/started $1 ]
    RTN

  ( -- )
  &read-set
    ;game/set DUP2 #0100 ADD2 SWP2
    &read-set/loop
      ;&read-set-row JSR2 NOT ,&read-set/done JCN
      GTH2k ,&read-set/loop JCN
    &read-set/done
    POP2
    ;&get-line JSR2
    RTN

  ( row* -- row* success )
  &read-set-row
    STH2
    #00 [ ;&read-set-row/started STA ]
    &read-set-row/loop
      ;&get-byte JSR2
      DUP LIT '0 LTH ,&read-set-row/done JCN
      #01 [ ;&read-set-row/started STA ]
      LIT '0 SUB ( '0 -> 0 )
      DUP #00 EQU ,&read-set-row/not-letter JCN
        ( LIT '0 ADD LIT 'a SUB INC )
        #30 SUB ( 'a -> 1 )
      &read-set-row/not-letter
      STH2rk STA
      INC2r
      ,&read-set-row/loop JMP

    &read-set-row/done
    POP
    STH2r
    LIT [ &read-set-row/started $1 ]
    RTN

  ( -- n success )
  &read-number
    LIT2r 0000 ( : n )
    #00 [ ;&read-number/started STA ]
    &read-number/loop
      ;&get-byte JSR2
      DUP LIT '0 LTH ,&read-number/done JCN
      LIT '0 SUB #00 SWP ( value : n )
      STH2r #000a MUL2 ADD2 STH2 ( : n*10+value )
      #01 [ ;&read-number/started STA ]
      ,&read-number/loop JMP
    &read-number/done
    ( terminator )
    POP
    STH2r
    [ ;&read-number/started LDA ]
    RTN
    [ &read-number/started $1 ]

  ( -- )
  &get-line
    LIT2r :&line
    &get-line/next-byte
      ;&read-byte JSR2
      DUP #00 EQU ,&get-line/eof JCN
      DUP #0a EQU ,&get-line/done JCN
      STH2rk STA INC2r
      ,&get-line/next-byte JMP
    &get-line/done
    POP #00 STH2r STA
    #00
    RTN
    &get-line/eof
    POP POP2r
    #01
    RTN

  ( filename* -- )
  &open-file
    .File/name DEO2
    #0001 .File/length DEO2
    RTN

  ( -- next-byte )
  &get-byte
  &read-byte
    #00 ;&byte STA ( clear byte so that EOF reads as zero )
    ;&byte .File/read DEO2
    LIT [ &byte 00 ] ( <- this byte is patched by the two previous instructions! )
    RTN

  [
    &line $1000
  ]

@game
  &render
    ( draw tiles )
    #0000 #0010 DO
      DUP2 8** .Screen/y DEO2
      #0000 #0010 DO
        ( y x )
        DUP2 8** .Screen/x DEO2

        OVR2 10** OVR2 ADD2 ;&set ADD2 LDA
        #00 SWP 8** ;&tiles ADD2 .Screen/addr DEO2

        #0e .Screen/sprite DEO
      LOOP
    LOOP

    ( draw sprites )
    [ ;&sprite-count LDA ]
    &render/sprite-loop
      DEC
      DUP #00 SWP 2** ;&sprite-pos ADD2 LDA2 ( x y )
      #00 SWP 8** .Screen/y DEO2
      #00 SWP 8** .Screen/x DEO2

      DUP #00 SWP 8** ;&sprites ADD2 .Screen/addr DEO2

      #0d .Screen/sprite DEO

      DUP ,&render/sprite-loop JCN
    POP

    .Controller/button DEI

    DUP ,&any-button-down JCN
      #00 [ ;&cooldown STA ]
      ;&move-done JMP2

    &any-button-down

    [ ;&cooldown LDA ] NOT ,&cooled-down JCN
      [ ;&cooldown *DEC ]
      ;&move-done JMP2

    &cooled-down

    DUP #80 AND NOT ,&not-right JCN
      ;&avatar-x LDA
      DUP #0e GTH ,&not-right JCN
      INC ;&avatar-x STA
      ,&moved JMP
    &not-right

    DUP #40 AND NOT ,&not-left JCN
      ;&avatar-x LDA
      DUP #01 LTH ,&not-left JCN
      DEC ;&avatar-x STA
      ,&moved JMP
    &not-left

    DUP #20 AND NOT ,&not-down JCN
      ;&avatar-y LDA
      DUP #0e GTH ,&not-down JCN
      INC ;&avatar-y STA
      ,&moved JMP
    &not-down

    DUP #10 AND NOT ,&not-up JCN
      ;&avatar-y LDA
      DUP #01 LTH ,&not-up JCN
      DEC ;&avatar-y STA
      ,&moved JMP
    &not-up

    ,&move-done JMP

    &moved
    #08 [ ;&cooldown STA ]

    &move-done
    POP

    RTN
  [
    &set
      $1000
    &tiles
      00 00 00 00 00 00 00 00
      $1000
    &sprites
      $1000
    &sprite-count 00
    &sprite-pos
      &avatar-x $1 &avatar-y $1
      $200
    &cooldown 00
  ]
